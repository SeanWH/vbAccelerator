<html lang="en" >
<head>

<title>vbAccelerator - Forcing a local or remote NT system to reboot</title><meta http-equiv="Content-Type" content="text/html;charset=utf-8" /><meta name="AUTHOR" content="Steve McMahon" /><meta name="KEYWORDS" content=".NET, .NET Framework, C#, CSharp, VB, Visual Basic, VB5, VB6, Visual Basic, Active X, Controls, Components" /><meta name="DESCRIPTION" content="Under Windows NT, you can force a timed system shutdown on either the local machine or a remote network machine. This code tip shows how to do it. You can specifiy how long it will be before the machine will be shutdown in seconds (a zero value shuts down immediately), how remorseless the shutdown process should be (whether it allows any unsaved work to be saved) and whether the machine will be rebooted." /><link rel="stylesheet" href="..\..\..\res\screen.css" media="SCREEN" /><link rel="stylesheet" href="..\..\..\res\print.css" media="PRINT" /></head><body>
<table>
<tr class="adbar" bgcolor="#83726a"><td colspan="3">
<!-- TF 468x60 JScript HORIZ NoPop code -->
<script language=javascript><!--
document.write('<scr'+'ipt language=javascript src="http://a.tribalfusion.com/j.ad?site=VBAcceleratorcom&adSpace=ROS&size=468x60&type=horiz&pop=0&requestID='+((new Date()).getTime() % 2147483648) + Math.random()+'"></scr'+'ipt>');
//-->
</script>
<noscript>
   <a href="javascript:window.alert(&quot;http://a.tribalfusion.com/i.click?site=VBAcceleratorcom&adSpace=ROS&size=468x60&requestID=1423477941\nThis link was not retrieved.&quot;)" target=_blank>
   <img src="http://cdn5.tribalfusion.com/media/493786.gif" 
                  width=468 height=60 border=0 alt="Click Here"></a>
</noscript>
<!-- TF 468x60 JScript HORIZ NoPop code -->
</td></tr><tr class="navbar"><td><a href="..\..\..\index.html"><img width="125" height="25" src="..\..\..\res\vbaccelerator.png" alt="vbAccelerator Logo" /></a></td><td valign="bottom"><p class="nav"><a href="..\..\..\index.html">Home</a>&#160;.&#160;<a href="..\..\index.html">VB</a>&#160;.&#160;<a href="..\index.asp\index.html">Tips</a>&#160;.&#160;Forcing a local or remote NT system to reboot</p></td><td></td></tr><tr class="navbar"><td colspan="3"><hr /></td></tr><tr valign="top"><td class="sidebar"><img src="..\..\..\res\information.png" width="125" height="21" alt="Information" /><p class="nav">Article:1383</p><p class="nav">&#160;&#160;<a href="..\..\..\..\linkto_asp\id=1383&type=article&title=forcing_20a_20local_20or_20remote_20nt_20system_20to_20reboot.html">Link to this page</a></p><p class="nav">Applies To:</p><p class="nav">&#160;&#160;VB5</p><p class="nav">&#160;&#160;VB6</p><p class="nav">Author:</p><p class="nav">&#160;&#160;<a href="mailto:steve@vbaccelerator.com">Steve McMahon</a></p><br /><br /><img src="..\..\..\res\bugtrak.png" width="125" height="21" alt="BugTrak System" /><p class="nav">No logged bugs.</p><br /><br /><img src="..\..\..\res\updates.png" width="125" height="21" alt="Updates" /><p class="nav"><img src="..\..\..\res\update.png" width="8" height="8" alt="Update" />9 Aug 1998<br />First Posted</p><br /><br /><img src="..\..\..\res\related.png" width="125" height="21" alt="Related Items" /><br /><br /><img src="..\..\..\res\search.png" width="125" height="21" alt="Search" />
<!-- Search Google -->
<form method="GET" action="http://www.google.com/custom">
<a href="javascript:window.alert(&quot;http://www.google.com/search\nThis link was not retrieved.&quot;)"><img src="..\..\..\..\..\www_google_com\logos\logo_40wht.gif" alt="Google" /></A><br />
<input type="text" name="q" size="31" maxlength="255" value="" class="search" /><br />
<input type="submit" name="sa" value="Search Google" class="search" />
<input type="hidden" name="cof" value="T:black;LW:330;ALC:990000;L:http://www.vbaccelerator.com/images/accl9.gif;LC:660000;LH:66;BGC:white;AH:left;VLC:999966;S:http://vbaccelerator.com/;GALT:999966;AWFID:67c7759ff27e5395;" />
<input type="hidden" name="domains" value="vbaccelerator.com" /><br />
<p class="nav"><input type="radio" name="sitesearch" value="" />Search WWW<br /></p>
<p class="nav"><input type="radio" name="sitesearch" value="vbaccelerator.com" checked="true" />vbAccelerator<br /></p>
</FORM>
<!-- Search Google -->
<br /><br />

<a href="..\..\..\the_site\newsite\article.html"><img src="..\..\..\res\newsite.png" width="125" height="129" alt="The New Site" /></a>
<br /><br />
</td><td width="100%" valign="top"><h1>Forcing a local or remote NT system to reboot</h1><p /><p>Under Windows NT, you can force a timed system shutdown on either the local machine or a remote network machine. This code tip shows how to do it. You can specifiy how long it will be before the machine will be shutdown in seconds (a zero value shuts down immediately), how remorseless the shutdown process should be (whether it allows any unsaved work to be saved) and whether the machine will be rebooted.</p><p>Start a new project in VB. Add a new module, then add the following code to it:</p><pre>
' To Determine if we are running NT or not: 
Private Type OSVERSIONINFO 
    dwOSVersionInfoSize As Long 
    dwMajorVersion As Long 
    dwMinorVersion As Long 
    dwBuildNumber As Long 
    dwPlatformId As Long 
    szCSDVersion As String * 128 ' Maintenance string for PSS usage 
End Type 
Private Declare Function GetVersionEx Lib "kernel32" Alias "GetVersionExA" _
       (lpVersionInformation As OSVERSIONINFO) As Long 
Private Const VER_PLATFORM_WIN32_NT = 2 
Private Const VER_PLATFORM_WIN32_WINDOWS = 1 
Private Const VER_PLATFORM_WIN32s = 0 

' To Report API errors: 
Private Const FORMAT_MESSAGE_ALLOCATE_BUFFER = &amp;H100 
Private Const FORMAT_MESSAGE_ARGUMENT_ARRAY = &amp;H2000 
Private Const FORMAT_MESSAGE_FROM_HMODULE = &amp;H800 
Private Const FORMAT_MESSAGE_FROM_STRING = &amp;H400 
Private Const FORMAT_MESSAGE_FROM_SYSTEM = &amp;H1000 
Private Const FORMAT_MESSAGE_IGNORE_INSERTS = &amp;H200 
Private Const FORMAT_MESSAGE_MAX_WIDTH_MASK = &amp;HFF 
Private Declare Function FormatMessage Lib "kernel32" _
      Alias "FormatMessageA" ( _
      ByVal dwFlags As Long, lpSource As Any, _
      ByVal dwMessageId As Long, ByVal dwLanguageId As Long, _
      ByVal lpBuffer As String, ByVal nSize As Long, _
      Arguments As Long) As Long 


' =============================================================
' NT Only 
Private Type LARGE_INTEGER 
    LowPart As Long 
    HighPart As Long 
End Type 
Private Type LUID 
    LowPart As Long 
    HighPart As Long 
End Type 
Private Type LUID_AND_ATTRIBUTES 
    pLuid As LUID 
    Attributes As Long 
End Type 
Private Type TOKEN_PRIVILEGES 
    PrivilegeCount As Long 
    Privileges(0 To 0) As LUID_AND_ATTRIBUTES 
End Type 
Private Declare Function GetCurrentProcess Lib "kernel32" () As Long 
Private Declare Function OpenProcessToken Lib "advapi32.dll" _
      (ByVal ProcessHandle As Long, ByVal DesiredAccess As Long, _
      TokenHandle As Long) As Long 
Private Declare Function CloseHandle Lib "kernel32" (ByVal hObject As Long) As Long 
Private Declare Function GetTokenInformation Lib "advapi32.dll" _
      (ByVal TokenHandle As Long, TokenInformationClass As Integer, _
      TokenInformation As Any, ByVal TokenInformationLength As Long, _
      ReturnLength As Long) As Long 
Private Declare Function AdjustTokenPrivileges Lib "advapi32.dll" _
      (ByVal TokenHandle As Long, ByVal DisableAllPrivileges As Long, _
      NewState As TOKEN_PRIVILEGES, ByVal BufferLength As Long, _
      PreviousState As TOKEN_PRIVILEGES, ReturnLength As Long) As Long 
Private Declare Function LookupPrivilegeValue Lib "advapi32.dll" _
      Alias "LookupPrivilegeValueA" _
      (ByVal lpSystemName As String, ByVal lpName As String, lpLuid As LUID) As Long 
Private Const SE_SHUTDOWN_NAME = "SeShutdownPrivilege" 
Private Const SE_PRIVILEGE_ENABLED = &amp;H2 

Private Const READ_CONTROL = &amp;H20000 
Private Const STANDARD_RIGHTS_ALL = &amp;H1F0000 
Private Const STANDARD_RIGHTS_EXECUTE = (READ_CONTROL) 
Private Const STANDARD_RIGHTS_READ = (READ_CONTROL) 
Private Const STANDARD_RIGHTS_REQUIRED = &amp;HF0000 
Private Const STANDARD_RIGHTS_WRITE = (READ_CONTROL) 

Private Const TOKEN_ASSIGN_PRIMARY = &amp;H1 
Private Const TOKEN_DUPLICATE = (&amp;H2) 
Private Const TOKEN_IMPERSONATE = (&amp;H4) 
Private Const TOKEN_QUERY = (&amp;H8) 
Private Const TOKEN_QUERY_SOURCE = (&amp;H10) 
Private Const TOKEN_ADJUST_PRIVILEGES = (&amp;H20) 
Private Const TOKEN_ADJUST_GROUPS = (&amp;H40) 
Private Const TOKEN_ADJUST_DEFAULT = (&amp;H80) 
Private Const TOKEN_ALL_ACCESS = (STANDARD_RIGHTS_REQUIRED Or _ 
                        TOKEN_ASSIGN_PRIMARY Or _ 
                        TOKEN_DUPLICATE Or _ 
                        TOKEN_IMPERSONATE Or _ 
                        TOKEN_QUERY Or _ 
                        TOKEN_QUERY_SOURCE Or _ 
                        TOKEN_ADJUST_PRIVILEGES Or _ 
                        TOKEN_ADJUST_GROUPS Or _ 
                        TOKEN_ADJUST_DEFAULT) 
Private Const TOKEN_READ = (STANDARD_RIGHTS_READ Or _ 
                        TOKEN_QUERY) 
Private Const TOKEN_WRITE = (STANDARD_RIGHTS_WRITE Or _ 
                        TOKEN_ADJUST_PRIVILEGES Or _ 
                        TOKEN_ADJUST_GROUPS Or _ 
                        TOKEN_ADJUST_DEFAULT) 
Private Const TOKEN_EXECUTE = (STANDARD_RIGHTS_EXECUTE) 

Private Const TokenDefaultDacl = 6 
Private Const TokenGroups = 2 
Private Const TokenImpersonationLevel = 9 
Private Const TokenOwner = 4 
Private Const TokenPrimaryGroup = 5 
Private Const TokenPrivileges = 3 
Private Const TokenSource = 7 
Private Const TokenStatistics = 10 
Private Const TokenType = 8 
Private Const TokenUser = 1 

Private Declare Function InitiateSystemShutdown Lib "advapi32.dll" _
      Alias "InitiateSystemShutdownA" _
      (ByVal lpMachineName As String, ByVal lpMessage As String, _
      ByVal dwTimeout As Long, ByVal bForceAppsClosed As Long, _
      ByVal bRebootAfterShutdown As Long) As Long 
Private Declare Function AbortSystemShutdown Lib "advapi32.dll" _
      Alias "AbortSystemShutdownA" _
      (ByVal lpMachineName As String) As Long 
' ==========================================================

Public Function WinError(ByVal lLastDLLError As Long) As String 
Dim sBuff As String 
Dim lCount As Long 
    
    ' Return the error message associated with LastDLLError: 
    sBuff = String$(256, 0) 
    lCount = FormatMessage( _ 
    FORMAT_MESSAGE_FROM_SYSTEM Or FORMAT_MESSAGE_IGNORE_INSERTS, _ 
    0, lLastDLLError, 0&amp;, sBuff, Len(sBuff), ByVal 0) 
    If lCount Then 
    WinError = Left$(sBuff, lCount) 
    End If 
    
End Function 

Public Function IsNT() As Boolean 
Static bOnce As Boolean 
Static bValue As Boolean 

    ' Return whether the system is running NT or not: 
    If Not (bOnce) Then 
    Dim tVI As OSVERSIONINFO 
    tVI.dwOSVersionInfoSize = Len(tVI) 
    If (GetVersionEx(tVI) &lt;&gt; 0) Then 
        bValue = (tVI.dwPlatformId = VER_PLATFORM_WIN32_NT) 
        bOnce = True 
    End If 
    End If 
    IsNT = bValue 
    
End Function 

Private Function NTEnableShutDown(ByRef sMsg As String) As Boolean 
Dim tLUID As LUID 
Dim hProcess As Long 
Dim hToken As Long 
Dim tTP As TOKEN_PRIVILEGES, tTPOld As TOKEN_PRIVILEGES 
Dim lTpOld As Long 
Dim lR As Long 

    ' Under NT we must enable the SE_SHUTDOWN_NAME privilege in the 
    ' process we're trying to shutdown from, otherwise a call to 
    ' try to shutdown has no effect! 

    ' Find the LUID of the Shutdown privilege token: 
    lR = LookupPrivilegeValue(vbNullString, SE_SHUTDOWN_NAME, tLUID) 
    
    ' If we get it: 
    If (lR &lt;&gt; 0) Then 
                
      ' Get the current process handle: 
       hProcess = GetCurrentProcess() 
       If (hProcess &lt;&gt; 0) Then 
          ' Open the token for adjusting and querying (if we can - user may not have rights): 
          lR = OpenProcessToken(hProcess, TOKEN_ADJUST_PRIVILEGES Or TOKEN_QUERY, hToken) 
          If (lR &lt;&gt; 0) Then 
                    
             ' Ok we can now adjust the shutdown priviledges: 
             With tTP 
                .PrivilegeCount = 1 
                With .Privileges(0) 
                   .Attributes = SE_PRIVILEGE_ENABLED 
                   .pLuid.HighPart = tLUID.HighPart 
                   .pLuid.LowPart = tLUID.LowPart 
                End With 
              End With 
            
              ' Now allow this process to shutdown the system: 
              lR = AdjustTokenPrivileges(hToken, 0, tTP, Len(tTP), tTPOld, lTpOld) 
            
              If (lR &lt;&gt; 0) Then 
                 NTEnableShutDown = True 
              Else 
                 Err.Raise eeSSDErrorBase + 6, App.EXEName &amp; ".mShutDown", _
                   "Can't enable shutdown: You do not have the privileges to shutdown this system. [" &amp; _
                    WinError(Err.LastDllError) &amp; "]" 
              End If 
            
              ' Remember to close the handle when finished with it: 
              CloseHandle hToken 
           Else 
              Err.Raise eeSSDErrorBase + 6, App.EXEName &amp; ".mShutDown", _
                 "Can't enable shutdown: You do not have the privileges to shutdown this system. [" &amp; _
                 WinError(Err.LastDllError) &amp; "]" 
           End If 
        Else 
           Err.Raise eeSSDErrorBase + 5, App.EXEName &amp; ".mShutDown", _
              "Can't enable shutdown: Can't determine the current process. [" &amp; _
              WinError(Err.LastDllError) &amp; "]" 
        End If 
    Else 
        Err.Raise eeSSDErrorBase + 4, App.EXEName &amp; ".mShutDown", _
           "Can't enable shutdown: Can't find the SE_SHUTDOWN_NAME privilege value. [" _
           &amp; WinError(Err.LastDllError) &amp; "]" 
    End If 

End Function 

Public Function NTForceTimedShutdown( _ 
    Optional ByVal lTimeOut As Long = -1, _ 
    Optional ByVal sMsg As String = "", _ 
    Optional ByVal sMachineNetworkName As String = vbNullString, _ 
    Optional ByVal bForceAppsToClose As Boolean = False, _ 
    Optional ByVal bReboot As Boolean = False _ 
    ) As Boolean 
Dim lR As Long 
    
    If IsNT Then 
    ' Make sure we have enabled the privilege to shutdown 
    ' for this process if we're running NT: 
    If Not (NTEnableShutDown(sMsg)) Then 
        Exit Function 
    End If 
    
    ' This is the code to do a timed shutdown: 
    lR = InitiateSystemShutdown(sMachineNetworkName, sMsg, lTimeOut, bForceAppsToClose, bReboot) 
    If (lR = 0) Then 
        Err.Raise eeSSDErrorBase + 2, App.EXEName &amp; ".mShutDown", _
            "InitiateSystemShutdown failed: " &amp; WinError(Err.LastDllError) 
    End If 
    
    Else 
       Err.Raise eeSSDErrorBase + 1, App.EXEName &amp; ".mShutDown", _
           "Function only available under Windows NT." 
    End If 
    
End Function 

Public Function NTAbortTimedShutdown( _ 
    Optional ByVal sMachineNetworkName As String = vbNullString _ 
) 
   AbortSystemShutdown sMachineNetworkName 
End Function 
</pre><p>
To try out a shutdown, add two Command buttons and a Text box to the project's form and then paste in the following code. Note you should save your work before running it, because the shutdown will kill VB without asking you to save any changes! Clicking on the first Command button will initiate a shutdown, with a timeout value set to the number of seconds entered into the text box. To stop the shutdown, click the second Command button.
</p><pre>
Private Sub Command1_Click() 
   If (MsgBox("Are you sure you want to initiate a forced, timed shutdown?", _
          vbYesNo Or vbQuestion) = vbYes) Then 
      NTForceTimedShutdown CLng(Text1.Text), _
          "You're gonna get shutdown in " &amp; Text1.Text &amp; " s..." 
   End If 
End Sub 

Private Sub Command2_Click() 
   NTAbortTimedShutdown 
End Sub 

Private Sub Form_Load() 
   Text1.Text = 60 
End Sub 
</pre><p>&#160;</p><p>&#160;</p><p>&#160;</p><p>&#160;</p><p>&#160;</p><p>&#160;</p></td><td class="rightbar" valign="top" bgcolor="#C2BEB1">
<!-- No right bar content currently defined -->
</td></tr><tr class="footer"><td></td>
<td>
<p class="center">

<script type="text/javascript"><!--
google_ad_client = "pub-4690375644913255";
google_ad_width = 728;
google_ad_height = 90;
google_ad_format = "728x90_as";
google_ad_channel ="";
google_ad_type = "text_image";
google_color_border = "336699";
google_color_bg = "FFFFFF";
google_color_link = "0000FF";
google_color_url = "008000";
google_color_text = "000000";
//--></script>
<script type="text/javascript"
  src="http://pagead2.googlesyndication.com/pagead/show_ads.js">
</script>
<noscript>
<A HREF="..\..\code\vbmedia\using_gdi_plus\index.html" ><IMG SRC="..\..\..\res\gdiplus.png" ALT="A GDI+ Library for VB - read and write PNG, JPG, TIF and GIF files, and manipulate bitmaps quickly" WIDTH=468 HEIGHT=60 BORDER=0></A>
</noscript>

</p>
</td>
<td></td>
</tr><tr><td colspan="3"><hr /></td></tr><tr class="footer"><td valign="top"><a href="..\..\..\index.html"><img width="125" height="25" src="..\..\..\res\vbaccelerator.png" alt="vbAccelerator Logo" /></a></td><td valign="bottom"><p class="nav"><a href="..\..\..\index.html">Home</a>&#160;.&#160;<a href="..\..\index.html">VB</a>&#160;.&#160;<a href="..\index.asp\index.html">Tips</a>&#160;.&#160;Forcing a local or remote NT system to reboot</p><br /><p class="nav"><a href="..\..\..\the_site\copyright\article.html">Copyright</a> &#169; 1998-2003 Steve McMahon <a href="mailto:steve@vbaccelerator.com">steve@vbaccelerator.com</a>.  All rights reserved.<br />Last Updated: 28 November 2002</p></td><td></td></tr></table>
</body></html>
