<html lang="en" >
<head>

<title>vbAccelerator - Get System Display Fonts and Non-Client Area Sizes</title><meta http-equiv="Content-Type" content="text/html;charset=utf-8" /><meta name="AUTHOR" content="Steve McMahon" /><meta name="KEYWORDS" content=".NET, .NET Framework, C#, CSharp, VB, Visual Basic, VB5, VB6, Visual Basic, Active X, Controls, Components" /><meta name="DESCRIPTION" content="The Non-Client area of a window is defined as the area you don't draw on; 
i.e. the window border, caption, menu bar and scroll bars. Whilst it is easy to get the 
colours that Windows uses to display these items (the VB enumeration SystemColorConstants supplies 
all the OLE_COLOR versions of these colours, and these can easily be converted into 
an actual colour using the OLETranslateColor function, as described in the tip 
Get an RGB Colour from an OLE_COLOR), it isn't so straightforward to get the fonts and sizes." /><link rel="stylesheet" href="..\..\..\res\screen.css" media="SCREEN" /><link rel="stylesheet" href="..\..\..\res\print.css" media="PRINT" /></head><body>
<table>
<tr class="adbar" bgcolor="#83726a"><td colspan="3">
<!-- TF 468x60 JScript HORIZ NoPop code -->
<script language=javascript><!--
document.write('<scr'+'ipt language=javascript src="http://a.tribalfusion.com/j.ad?site=VBAcceleratorcom&adSpace=ROS&size=468x60&type=horiz&pop=0&requestID='+((new Date()).getTime() % 2147483648) + Math.random()+'"></scr'+'ipt>');
//-->
</script>
<noscript>
   <a href="javascript:window.alert(&quot;http://a.tribalfusion.com/i.click?site=VBAcceleratorcom&adSpace=ROS&size=468x60&requestID=1423477941\nThis link was not retrieved.&quot;)" target=_blank>
   <img src="http://cdn5.tribalfusion.com/media/493786.gif" 
                  width=468 height=60 border=0 alt="Click Here"></a>
</noscript>
<!-- TF 468x60 JScript HORIZ NoPop code -->
</td></tr><tr class="navbar"><td><a href="..\..\..\index.html"><img width="125" height="25" src="..\..\..\res\vbaccelerator.png" alt="vbAccelerator Logo" /></a></td><td valign="bottom"><p class="nav"><a href="..\..\..\index.html">Home</a>&#160;.&#160;<a href="..\..\index.html">VB</a>&#160;.&#160;<a href="..\index.asp\index.html">Tips</a>&#160;.&#160;Get System Display Fonts and Non-Client Area Sizes</p></td><td></td></tr><tr class="navbar"><td colspan="3"><hr /></td></tr><tr valign="top"><td class="sidebar"><img src="..\..\..\res\information.png" width="125" height="21" alt="Information" /><p class="nav">Article:2958</p><p class="nav">&#160;&#160;<a href="..\..\..\..\linkto_asp\id=2958&type=article&title=get_20system_20display_20fonts_20and_20non_2dclient_20area_20sizes.html">Link to this page</a></p><p class="nav">Applies To:</p><p class="nav">&#160;&#160;VB5</p><p class="nav">&#160;&#160;VB6</p><p class="nav">Author:</p><p class="nav">&#160;&#160;<a href="mailto:steve@vbaccelerator.com">Steve McMahon</a></p><br /><br /><img src="..\..\..\res\bugtrak.png" width="125" height="21" alt="BugTrak System" /><p class="nav">No logged bugs.</p><br /><br /><img src="..\..\..\res\updates.png" width="125" height="21" alt="Updates" /><p class="nav"><img src="..\..\..\res\update.png" width="8" height="8" alt="Update" />18 Aug 1998<br />First Posted</p><br /><br /><img src="..\..\..\res\related.png" width="125" height="21" alt="Related Items" /><p class="nav"><img src="..\..\..\res\rel.png" width="8" height="8" alt="Related Item" /><a href="..\get_an_rgb_color_from_an_ole_color\article.html">Get an RGB Colour from an OLE_COLOR</a></p><br /><br /><img src="..\..\..\res\search.png" width="125" height="21" alt="Search" />
<!-- Search Google -->
<form method="GET" action="http://www.google.com/custom">
<a href="javascript:window.alert(&quot;http://www.google.com/search\nThis link was not retrieved.&quot;)"><img src="..\..\..\..\..\www_google_com\logos\logo_40wht.gif" alt="Google" /></A><br />
<input type="text" name="q" size="31" maxlength="255" value="" class="search" /><br />
<input type="submit" name="sa" value="Search Google" class="search" />
<input type="hidden" name="cof" value="T:black;LW:330;ALC:990000;L:http://www.vbaccelerator.com/images/accl9.gif;LC:660000;LH:66;BGC:white;AH:left;VLC:999966;S:http://vbaccelerator.com/;GALT:999966;AWFID:67c7759ff27e5395;" />
<input type="hidden" name="domains" value="vbaccelerator.com" /><br />
<p class="nav"><input type="radio" name="sitesearch" value="" />Search WWW<br /></p>
<p class="nav"><input type="radio" name="sitesearch" value="vbaccelerator.com" checked="true" />vbAccelerator<br /></p>
</FORM>
<!-- Search Google -->
<br /><br />

<a href="..\..\..\the_site\newsite\article.html"><img src="..\..\..\res\newsite.png" width="125" height="129" alt="The New Site" /></a>
<br /><br />
</td><td width="100%" valign="top"><h1>Get System Display Fonts and Non-Client Area Sizes</h1><p /><p>The Non-Client area of a window is defined as the area you don't draw on; 
i.e. the window border, caption, menu bar and scroll bars. Whilst it is easy to get the 
colours that Windows uses to display these items (the VB enumeration <i>SystemColorConstants</i> supplies 
all the <i>OLE_COLOR</i> versions of these colours, and these can easily be converted into 
an actual colour using the <i>OLETranslateColor</i> function, as described in the tip 
<a href="..\get_an_rgb_color_from_an_ole_color\article.html">Get an RGB Colour from an OLE_COLOR</a>), it isn't so straightforward to get the fonts and sizes.</p><p>Information about the fonts and sizes windows uses to draw these areas can be 
accessed via the <i>SystemParametersInfo</i> call, passing the action code 
<i>SPI_GETNONCLIENTMETRICS</i>. The data is then passed back in a <i>NONCLIENTMETRICS</i> structure. 
This tip shows all the declares required to get this information and to interpret it 
into VB readable information.</p><p>Start a new project in VB, and add a class module. Set the class module's name to 
<i>cNCMetrics</i> and then add the following code to the class: </p><pre>
Private Const SPI_GETICONMETRICS = 45
Private Const SPI_GETICONTITLELOGFONT = 31
Private Const LF_FACESIZE = 32
Private Const LF_FULLFACESIZE = 64

' Normal log font structure:
Private Type LOGFONT
   lfHeight As Long
   lfWidth As Long
   lfEscapement As Long
   lfOrientation As Long
   lfWeight As Long
   lfItalic As Byte
   lfUnderline As Byte
   lfStrikeOut As Byte
   lfCharSet As Byte
   lfOutPrecision As Byte
   lfClipPrecision As Byte
   lfQuality As Byte
   lfPitchAndFamily As Byte
   lfFaceName(LF_FACESIZE) As Byte
End Type
Private Enum CNCMetricsFontWeightConstants
   FW_DONTCARE = 0
   FW_THIN = 100
   FW_EXTRALIGHT = 200
   FW_ULTRALIGHT = 200
   FW_LIGHT = 300
   FW_NORMAL = 400
   FW_REGULAR = 400
   FW_MEDIUM = 500
   FW_SEMIBOLD = 600
   FW_DEMIBOLD = 600
   FW_BOLD = 700
   FW_EXTRABOLD = 800
   FW_ULTRABOLD = 800
   FW_HEAVY = 900
   FW_BLACK = 900
End Enum
' For some bizarre reason, maybe to do with byte
' alignment, the LOGFONT structure we must apply
' to NONCLIENTMETRICS seems to require an LF_FACESIZE
' 4 bytes smaller than normal:
Private Type NMLOGFONT
   lfHeight As Long
   lfWidth As Long
   lfEscapement As Long
   lfOrientation As Long
   lfWeight As Long
   lfItalic As Byte
   lfUnderline As Byte
   lfStrikeOut As Byte
   lfCharSet As Byte
   lfOutPrecision As Byte
   lfClipPrecision As Byte
   lfQuality As Byte
   lfPitchAndFamily As Byte
   lfFaceName(LF_FACESIZE - 4) As Byte
End Type
Private Type NONCLIENTMETRICS
   cbSize As Long
   iBorderWidth As Long
   iScrollWidth As Long
   iScrollHeight As Long
   iCaptionWidth As Long
   iCaptionHeight As Long
   lfCaptionFont As NMLOGFONT
   iSMCaptionWidth As Long
   iSMCaptionHeight As Long
   lfSMCaptionFont As NMLOGFONT
   iMenuWidth As Long
   iMenuHeight As Long
   lfMenuFont As NMLOGFONT
   lfStatusFont As NMLOGFONT
   lfMessageFont As NMLOGFONT
End Type
Private Const SPI_GETNONCLIENTMETRICS = 41
Private Const SPI_SETNONCLIENTMETRICS = 42
Private Declare Function SystemParametersInfo Lib "user32" Alias "SystemParametersInfoA" ( _
   ByVal uAction As Long, _
   ByVal uParam As Long, 
_    lpvParam As Any, _
   ByVal fuWinIni As Long) As Long
Private Declare Sub CopyMemory Lib "kernel32" Alias "RtlMoveMemory" ( _
   lpvDest As Any, lpvSource As Any, ByVal cbCopy As Long)
Private Declare Function GetDeviceCaps Lib "gdi32" (ByVal hDC As Long, ByVal nIndex As Long) As Long
Private Const LOGPIXELSY = 90 ' Logical pixels/inch in Y
Private Declare Function CreateFontIndirect Lib "gdi32" Alias "CreateFontIndirectA" (lpLogFont As LOGFONT) As Long
Private Declare Function DeleteObject Lib "gdi32" (ByVal hObject As Long) As Long

Private m_tNCM As NONCLIENTMETRICS
Private m_tLF As LOGFONT

Public Enum CNCMetricsFontTypes
   IconFont = 1
   CaptionFont = 2
   SMCaptionFont = 3
   MenuFOnt = 4
   StatusFont = 5
   MessageFont = 6
End Enum

Public Function GetMetrics() As Boolean
Dim lR As Long
   ' Get Non-client metrics:
   m_tNCM.cbSize = 340 
   lR = SystemParametersInfo( _
     SPI_GETNONCLIENTMETRICS, _
     0, _
     m_tNCM, _
     0)
   If (lR &lt;&gt; 0) Then
     ' Get icon font:
     lR = SystemParametersInfo( _
       SPI_GETICONTITLELOGFONT, _
       0, _
       m_tLF, _
       0)
     GetMetrics = (lR &lt;&gt; 0)
   End If
End Function
Property Get Font( _
     ByVal hDC As Long, _
     ByVal eFontNum As CNCMetricsFontTypes _
   ) As StdFont
Dim lR As Long
Dim tLF As LOGFONT

   Select Case eFontNum
   Case StatusFont
     CopyMemory tLF, m_tNCM.lfStatusFont, LenB(m_tNCM.lfStatusFont)
   Case SMCaptionFont
     CopyMemory tLF, m_tNCM.lfSMCaptionFont, LenB(m_tNCM.lfSMCaptionFont)
   Case MessageFont
     CopyMemory tLF, m_tNCM.lfMessageFont, LenB(m_tNCM.lfMessageFont)
   Case MenuFOnt
     CopyMemory tLF, m_tNCM.lfMenuFont, LenB(m_tNCM.lfMenuFont)
   Case IconFont
     CopyMemory tLF, m_tLF, LenB(m_tLF)
   Case CaptionFont
     CopyMemory tLF, m_tNCM.lfCaptionFont, LenB(m_tNCM.lfCaptionFont)
   Case Else
     Exit Property
   End Select

   ' This demonstrates how to return a VB style font.
   ' If you want an API hFont, just do this:
   ' hFont = CreateFontIndirect(tLF)
   ' Remember to use DeleteObject hFont when you've
   ' finished with it.
   Dim sFnt As New StdFont
   pLogFontToStdFont tLF, hDC, sFnt
   Set Font = sFnt

End Property
Private Sub pLogFontToStdFont(ByRef tLF As LOGFONT, ByVal hDC As Long, ByRef sFnt As StdFont)
   With sFnt
     .Name = StrConv(tLF.lfFaceName, vbUnicode)
     If tLF.lfHeight &lt; 1 Then
       .Size = Abs((72# / GetDeviceCaps(hDC, LOGPIXELSY)) * tLF.lfHeight)
     Else
       .Size = tLF.lfHeight
     End If
     .Charset = tLF.lfCharSet
     .Italic = Not (tLF.lfItalic = 0)
     .Underline = Not (tLF.lfUnderline = 0)
     .Strikethrough = Not (tLF.lfStrikeOut = 0)
     .Bold = (tLF.lfWeight &gt; FW_REGULAR)
   End With
End Sub
Property Get CaptionHeight() As Long
   CaptionHeight = m_tNCM.iCaptionHeight
End Property
Property Get CaptionWIdth() As Long
   CaptionWIdth = m_tNCM.iCaptionWidth
End Property
Property Get MenuHeight() As Long
   MenuHeight = m_tNCM.iMenuHeight
End Property
Property Get MenuWidth() As Long
   MenuWidth = m_tNCM.iMenuWidth
End Property
Property Get ScrollHeight() As Long
   ScrollHeight = m_tNCM.iScrollHeight
End Property
Property Get ScrollWidth() As Long
   ScrollWidth = m_tNCM.iScrollWidth
End Property
Property Get SMCaptionHeight() As Long
   SMCaptionHeight = m_tNCM.iSMCaptionHeight
End Property
Property Get SMCaptionWIdth() As Long
   SMCaptionWIdth = m_tNCM.iSMCaptionWidth
End Property
Property Get BorderWidth() As Long
   BorderWidth = m_tNCM.iBorderWidth
End Property
</pre><p>To try out the code, add a Label control to your test project's main form. Rename the Label 
control to lblSample, set the <i>AutoSize</i> property to True and then make five copies of it, 
creating a control array of lblSample() controls in a column.</p><p>Finally, add another Label control called lblInfo, and again set <i>AutoSize</i> to True. 
Then add this code to the form's Form_Load event:</p><pre>
Private Sub Form_Load()
   Dim cNC As New cNCMetrics
   cNC.GetMetrics
   Set lblSample(0).Font = cNC.Font(Me.hDC, CaptionFont)
   Set lblSample(1).Font = cNC.Font(Me.hDC, IconFont)
   Set lblSample(2).Font = cNC.Font(Me.hDC, MenuFOnt)
   Set lblSample(3).Font = cNC.Font(Me.hDC, MessageFont)
   Set lblSample(4).Font = cNC.Font(Me.hDC, SMCaptionFont)
   Set lblSample(5).Font = cNC.Font(Me.hDC, StatusFont)
   lblInfo.Caption = "BorderWidth: " &amp; cNC.BorderWidth _
     &amp; vbCrLf &amp; "Caption Height: " &amp; cNC.CaptionHeight _
     &amp; vbCrLf &amp; "Caption Width:" &amp; cNC.CaptionWIdth _
     &amp; vbCrLf &amp; "Menu Height:" &amp; cNC.MenuHeight _
     &amp; vbCrLf &amp; "Menu Width:" &amp; cNC.MenuWidth _
     &amp; vbCrLf &amp; "Scrollbar Height:" &amp; cNC.ScrollHeight _
     &amp; vbCrLf &amp; "Scrollbar Width:" &amp; cNC.ScrollWidth _
     &amp; vbCrLf &amp; "SmallCap Height:" &amp; cNC.SMCaptionHeight _
     &amp; vbCrLf &amp; "SmallCap Width:" &amp; cNC.SMCaptionWIdth
End Sub 
</pre><p>Run the project. The first six label controls will have their font changed 
to match the caption font, icon font, menu font, message box font, small caption font 
and status bar font respectively. The final label control will display the widths and 
heights of the various non-client areas of the window.</p><p>&#160;</p><p>&#160;</p><p>&#160;</p><p>&#160;</p><p>&#160;</p><p>&#160;</p></td><td class="rightbar" valign="top" bgcolor="#C2BEB1">
<!-- No right bar content currently defined -->
</td></tr><tr class="footer"><td></td>
<td>
<p class="center">

<script type="text/javascript"><!--
google_ad_client = "pub-4690375644913255";
google_ad_width = 728;
google_ad_height = 90;
google_ad_format = "728x90_as";
google_ad_channel ="";
google_ad_type = "text_image";
google_color_border = "336699";
google_color_bg = "FFFFFF";
google_color_link = "0000FF";
google_color_url = "008000";
google_color_text = "000000";
//--></script>
<script type="text/javascript"
  src="http://pagead2.googlesyndication.com/pagead/show_ads.js">
</script>
<noscript>
<A HREF="javascript:window.alert(&quot;http://vbaccelerator.com/home/include/adredir.asp?url=http://oneandone.co.uk/xml/init?k_id=5791003&image=http://oneandone.co.uk/xml/banner?size=1&number=6\nThis link was not retrieved.&quot;)" ><IMG SRC="..\..\..\..\..\oneandone_co_uk\xml\banner\size=1&number=6.html" ALT="1&1 Web Hosting" WIDTH=468 HEIGHT=60 BORDER=0></A>
</noscript>

</p>
</td>
<td></td>
</tr><tr><td colspan="3"><hr /></td></tr><tr class="footer"><td valign="top"><a href="..\..\..\index.html"><img width="125" height="25" src="..\..\..\res\vbaccelerator.png" alt="vbAccelerator Logo" /></a></td><td valign="bottom"><p class="nav"><a href="..\..\..\index.html">Home</a>&#160;.&#160;<a href="..\..\index.html">VB</a>&#160;.&#160;<a href="..\index.asp\index.html">Tips</a>&#160;.&#160;Get System Display Fonts and Non-Client Area Sizes</p><br /><p class="nav"><a href="..\..\..\the_site\copyright\article.html">Copyright</a> &#169; 2003 Steve McMahon <a href="mailto:steve@vbaccelerator.com">steve@vbaccelerator.com</a>.  All rights reserved.<br />Last Updated: 8 January 2003</p></td><td></td></tr></table>
</body></html>
