<html lang="en" >
<head>
<title>vbAccelerator - Contents of code file: PopupControlVB_PopupWindowHelper.vb</title><meta http-equiv="Content-Type" content="text/html;charset=utf-8" /><meta name="AUTHOR" content="Steve McMahon" /><meta name="KEYWORDS" content=".NET, .NET Framework, C#, CSharp, VB, Visual Basic, VB5, VB6, Visual Basic, Active X, Controls, Components" /><meta name="DESCRIPTION" content="vbAccelerator - Contents of code file: PopupControlVB_PopupWindowHelper.vb" /><link rel="stylesheet" href="..\..\..\..\..\res\screen.css" media="SCREEN" /><link rel="stylesheet" href="..\..\..\..\..\res\print.css" media="PRINT" /></head><body>
<table>
<tr class="adbar" bgcolor="#83726a"><td colspan="3">
<!-- TF 468x60 JScript HORIZ NoPop code -->
<script language=javascript><!--
document.write('<scr'+'ipt language=javascript src="http://a.tribalfusion.com/j.ad?site=VBAcceleratorcom&adSpace=ROS&size=468x60&type=horiz&pop=0&requestID='+((new Date()).getTime() % 2147483648) + Math.random()+'"></scr'+'ipt>');
//-->
</script>
<noscript>
   <a href="javascript:window.alert(&quot;http://a.tribalfusion.com/i.click?site=VBAcceleratorcom&adSpace=ROS&size=468x60&requestID=1423477941\nThis link was not retrieved.&quot;)" target=_blank>
   <img src="http://cdn5.tribalfusion.com/media/493786.gif" 
                  width=468 height=60 border=0 alt="Click Here"></a>
</noscript>
<!-- TF 468x60 JScript HORIZ NoPop code -->
</td></tr><tr class="navbar"><td><a href="..\..\..\..\..\index.html"><img width="125" height="25" src="..\..\..\..\..\res\vbaccelerator.png" alt="vbAccelerator Logo" /></a></td><td valign="bottom"><p class="nav"><a href="..\..\..\..\..\index.html">Home</a>&#160;.&#160;<a href="..\..\..\..\index.html">NET</a>&#160;.&#160;<a href="..\..\..\index.html">Code</a>&#160;.&#160;<a href="..\..\index.html">Controls</a>&#160;.&#160;<a href="..\index.html">Popup Windows</a>&#160;.&#160;<a href="article.html">Use .NET Forms as Popup Windows</a>&#160;.&#160;<a href="popup_form_demonstration.html">Popup Form Demonstration</a>&#160;.&#160;PopupControlVB_PopupWindowHelper.vb</p></td><td></td></tr><tr class="navbar"><td colspan="3"><hr /></td></tr><tr valign="top"><td class="sidebar"><img src="..\..\..\..\..\res\download.png" width="125" height="21" alt="Downloads" /><p class="nav"><a href="popup_form_demonstration.html"><img src="..\..\..\..\..\res\get.png" width="8" height="8" alt="Download Page" />Popup Form Demonstration</a> (62K)</p><br /><br /><img src="..\..\..\..\..\res\information.png" width="125" height="21" alt="Information" /><p class="nav">Zip:13311</p><p class="nav">&#160;&#160;<a href="..\..\..\..\..\..\linkto_asp\id=13311&type=zip&title=popup_20form_20demonstration_2ezip_5fpopupcontrolvb_5fpopupwindowhelper.html">Link to code Zip</a></p><p class="nav">Applies To:</p><p class="nav">&#160;&#160;.NET</p><p class="nav">&#160;&#160;C#</p><p class="nav">&#160;&#160;VB.NET</p><p class="nav">Author:</p><p class="nav">&#160;&#160;<a href="mailto:steve@vbaccelerator.com">Steve McMahon</a></p><br /><br /><img src="..\..\..\..\..\res\bugtrak.png" width="125" height="21" alt="BugTrak System" /><p class="nav"><a href="bugtrak.html">BugTrak</a></p><p class="nav"><a href="bugtrak.html#bugs"><img src="..\..\..\..\..\res\btbug.png" width="16" height="16" alt="Bug" />&#160;Bugs:</a> 1 / 1</p><p class="nav"><a href="bugtrak.html#issues"><img src="..\..\..\..\..\res\btissue.png" width="16" height="16" alt="Issue" />&#160;Issues:</a> 0 / 0</p><p class="nav"><a href="bugtrak.html#questions"><img src="..\..\..\..\..\res\btquestion.png" width="16" height="16" alt="Question" />&#160;Questions:</a> 0 / 0</a></p><p class="nav">Updated:8 January 2004</p>
<br /><br /><img src="..\..\..\..\..\res\updates.png" width="125" height="21" alt="Updates" /><p class="nav"><img src="..\..\..\..\..\res\update.png" width="8" height="8" alt="Update" />1 Nov 2003<br />First Posted</p><br /><br /><img src="..\..\..\..\..\res\related.png" width="125" height="21" alt="Related Items" /><p class="nav"><img src="..\..\..\..\..\res\rel.png" width="8" height="8" alt="Related Item" /><a href="..\..\..\libraries\windows_messages\mouse_gestures\article.html">Adding Mouse Gesture Support to .NET Windows Applications</a></p><p class="nav"><img src="..\..\..\..\..\res\rel.png" width="8" height="8" alt="Related Item" /><a href="..\..\..\libraries\windows_messages\subclassing_in__net\article.html">Subclassing In .NET</a></p><p class="nav"><img src="..\..\..\..\..\res\rel.png" width="8" height="8" alt="Related Item" /><a href="..\..\..\..\..\vb\code\controls\drop_down_tool_windows\drop_down_form_control\article.html">vbAccelerator Drop-Down and Popup Form Control</a></p><br /><br /><img src="..\..\..\..\..\res\search.png" width="125" height="21" alt="Search" />
<!-- Search Google -->
<form method="GET" action="http://www.google.com/custom">
<a href="javascript:window.alert(&quot;http://www.google.com/search\nThis link was not retrieved.&quot;)"><img src="..\..\..\..\..\..\..\www_google_com\logos\logo_40wht.gif" alt="Google" /></A><br />
<input type="text" name="q" size="31" maxlength="255" value="" class="search" /><br />
<input type="submit" name="sa" value="Search Google" class="search" />
<input type="hidden" name="cof" value="T:black;LW:330;ALC:990000;L:http://www.vbaccelerator.com/images/accl9.gif;LC:660000;LH:66;BGC:white;AH:left;VLC:999966;S:http://vbaccelerator.com/;GALT:999966;AWFID:67c7759ff27e5395;" />
<input type="hidden" name="domains" value="vbaccelerator.com" /><br />
<p class="nav"><input type="radio" name="sitesearch" value="" />Search WWW<br /></p>
<p class="nav"><input type="radio" name="sitesearch" value="vbaccelerator.com" checked="true" />vbAccelerator<br /></p>
</FORM>
<!-- Search Google -->
<br /><br />

<a href="..\..\..\..\..\the_site\newsite\article.html"><img src="..\..\..\..\..\res\newsite.png" width="125" height="129" alt="The New Site" /></a>
<br /><br />
</td><td width="100%" valign="top"><h1>vbAccelerator - Contents of code file: PopupControlVB_PopupWindowHelper.vb</h1><pre>Namespace vbAccelerator.Components.Controls

#Region "Event Argument Classes"
    ''' &lt;summary&gt;
    ''' Contains event information for a &lt;see cref="PopupClosed"/&gt; event.
    ''' &lt;/summary&gt;
    Public Class PopupClosedEventArgs
        Inherits EventArgs

        ''' &lt;summary&gt;
        ''' The popup form.
        ''' &lt;/summary&gt;
        Private m_popup As Form = Nothing

        ''' &lt;summary&gt;
        ''' Gets the popup form which is being closed.
        ''' &lt;/summary&gt;
        Public ReadOnly Property Popup() As Form
            Get
                Return m_popup
            End Get
        End Property

        ''' &lt;summary&gt;
        ''' Constructs a new instance of this class for the specified
        ''' popup form.
        ''' &lt;/summary&gt;
        ''' &lt;param name="popup"&gt;Popup Form which is being closed.&lt;/param&gt;
        Public Sub New(ByVal popup As Form)
            m_popup = popup
        End Sub
    End Class

    ''' &lt;summary&gt;
    ''' Arguments to a &lt;see cref="PopupCancelEvent"/&gt;.  Provides a
    ''' reference to the popup form that is to be closed and 
    ''' allows the operation to be cancelled.
    ''' &lt;/summary&gt;
    Public Class PopupCancelEventArgs
        Inherits EventArgs

        ''' &lt;summary&gt;
        ''' Whether to cancel the operation
        ''' &lt;/summary&gt;
        Private m_cancel As Boolean = False
        ''' &lt;summary&gt;
        ''' Mouse down location
        ''' &lt;/summary&gt;
        Private location As Point
        ''' &lt;summary&gt;
        ''' Popup form.
        ''' &lt;/summary&gt;
        Private m_popup As Form = Nothing

        ''' &lt;summary&gt;
        ''' Constructs a new instance of this class.
        ''' &lt;/summary&gt;
        ''' &lt;param name="popup"&gt;The popup form&lt;/param&gt;
        ''' &lt;param name="location"&gt;The mouse location, if any, where the
        ''' mouse event that would cancel the popup occured.&lt;/param&gt;
        Public Sub New(ByVal popup As Form, ByVal location As Point)
            m_popup = popup
            Me.location = location
            m_cancel = False
        End Sub

        ''' &lt;summary&gt;
        ''' Gets the popup form
        ''' &lt;/summary&gt;
        Public ReadOnly Property Popup() As Form
            Get
                Return m_popup
            End Get
        End Property

        ''' &lt;summary&gt;
        ''' Gets the location that the mouse down which would cancel this 
        ''' popup occurred
        ''' &lt;/summary&gt;
        Public ReadOnly Property CursorLocation() As Point
            Get
                Return Me.location
            End Get
        End Property

        ''' &lt;summary&gt;
        ''' Gets/sets whether to cancel closing the form. Set to
        ''' &lt;c&gt;true&lt;/c&gt; to prevent the popup from being closed.
        ''' &lt;/summary&gt;
        Public Property Cancel() As Boolean
            Get
                Return m_cancel
            End Get
            Set(ByVal Value As Boolean)
                m_cancel = value
            End Set
        End Property

    End Class
#End Region

#Region "Delegates"
    ''' &lt;summary&gt;
    ''' Represents the method which responds to a &lt;see cref="PopupClosed"/&gt;
     event.
    ''' &lt;/summary&gt;
    Public Delegate Sub PopupClosedEventHandler(ByVal sender As Object, ByVal e
     As PopupClosedEventArgs)

    ''' &lt;summary&gt;
    ''' Represents the method which responds to a &lt;see cref="PopupCancel"/&gt;
     event.
    ''' &lt;/summary&gt;
    Public Delegate Sub PopupCancelEventHandler(ByVal sender As Object, ByVal e
     As PopupCancelEventArgs)
#End Region

#Region "PopupWindowHelper"
    ''' &lt;summary&gt;
    ''' A class to assist in creating popup windows like Combo Box drop-downs
     and Menus.
    ''' This class includes functionality to keep the title bar of the popup
     owner form
    ''' active whilst the popup is displayed, and to automatically cancel the
     popup
    ''' whenever the user clicks outside the popup window or shifts focus to
     another 
    ''' application.
    ''' &lt;/summary&gt;
    Public Class PopupWindowHelper
        Inherits NativeWindow

#Region "Unmanaged Code"
        Private Declare Auto Function SendMessage Lib "user32" ( _
            ByVal handle As IntPtr, _
            ByVal msg As Integer, _
            ByVal wParam As Integer, _
            ByVal lParam As IntPtr) As Integer

        Private Declare Auto Function PostMessage Lib "user32" ( _
            ByVal handle As IntPtr, _
            ByVal msg As Integer, _
            ByVal wParam As Integer, _
            ByVal lParam As IntPtr) As Integer

        Private Const WM_ACTIVATE As Integer = &amp;H6
        Private Const WM_ACTIVATEAPP As Integer = &amp;H1C
        Private Const WM_NCACTIVATE As Integer = &amp;H86

        Private Declare Sub keybd_event Lib "user32" ( _
            ByVal bVk As Byte, _
            ByVal bScan As Byte, _
            ByVal dwFlags As Integer, _
            ByVal dwExtraInfo As Integer)

        Private Const KEYEVENTF_KEYUP As Integer = &amp;H2
#End Region

#Region "Member Variables"
        ''' &lt;summary&gt;
        ''' Message filter to detect mouse clicks anywhere in the application
        ''' whilst the popup window is being displayed.
        ''' &lt;/summary&gt;
        Private WithEvents filter As PopupWindowHelperMessageFilter = Nothing
        ''' &lt;summary&gt;
        ''' The popup form that is being shown.
        ''' &lt;/summary&gt;
        Private WithEvents m_popup As Form = Nothing
        ''' &lt;summary&gt;
        ''' The owner of the popup form that is being shown:
        ''' &lt;/summary&gt;
        Private m_owner As Form = Nothing
        ''' &lt;summary&gt;
        ''' Whether the popup is showing or not.
        ''' &lt;/summary&gt;
        Private popupShowing As Boolean = False
        ''' &lt;summary&gt;
        ''' Whether the popup has been cancelled, notified by PopupCancel,
        ''' rather than closed.
        ''' &lt;/summary&gt;
        Private skipClose As Boolean = False
#End Region

        ''' &lt;summary&gt;
        ''' Raised when the popup form is closed.
        ''' &lt;/summary&gt;
        Public Event PopupClosed As PopupClosedEventHandler
        ''' &lt;summary&gt;
        ''' Raised when the Popup Window is about to be cancelled.  The
        ''' &lt;see cref="PopupCancelEventArgs.Cancel"/&gt; property can be
        ''' set to &lt;c&gt;true&lt;/c&gt; to prevent the form from being cancelled.
        ''' &lt;/summary&gt;
        Public Event PopupCancel As PopupCancelEventHandler

        ''' &lt;summary&gt;
        ''' Shows the specified Form as a popup window, keeping the
        ''' Owner's title bar active and preparing to cancel the popup
        ''' should the user click anywhere outside the popup window.
        ''' &lt;para&gt;Typical code to use this message is as follows:&lt;/para&gt;
        ''' &lt;code&gt;
        '''    frmPopup popup = new frmPopup();
        '''    Point location = Me.PointToScreen(new Point(button1.Left,
         button1.Bottom));
        '''    popupHelper.ShowPopup(this, popup, location);
        ''' &lt;/code&gt;
        ''' &lt;para&gt;Put as much initialisation code as possible
        ''' into the popup form's constructor, rather than the &lt;see
         cref="System.Windows.Forms.Load"/&gt;
        ''' event as this will improve visual appearance.&lt;/para&gt;
        ''' &lt;/summary&gt;
        ''' &lt;param name="owner"&gt;Main form which owns the popup&lt;/param&gt;
        ''' &lt;param name="popup"&gt;Window to show as a popup&lt;/param&gt;
        ''' &lt;param name="location"&gt;Location relative to the screen to show the
         popup at.&lt;/param&gt;
        Public Sub ShowPopup(ByVal owner As Form, ByVal popup As Form, ByVal
         location As Point)

            m_owner = owner
            m_popup = popup

            ' Start checking for the popup being cancelled
            Application.AddMessageFilter(filter)

            ' Set the location of the popup form:
            popup.StartPosition = FormStartPosition.Manual
            popup.Location = location
            ' Make it owned by the window that's displaying it:
            owner.AddOwnedForm(popup)

            ' Show the popup:
            Me.popupShowing = True
            popup.Show()
            popup.Activate()

            ' A little bit of fun.  We've shown the popup,
            ' but because we've kept the main window's
            ' title bar in focus the tab sequence isn't quite
            ' right.  This can be fixed by sending a tab,
            ' but that on its own would shift focus to the
            ' second control in the form.  So send a tab,
            ' followed by a reverse-tab.

            ' Send a Tab command:
            Dim bVk As Byte
            bVk = Keys.Tab
            keybd_event(bVk, 0, 0, 0)
            keybd_event(bVk, 0, KEYEVENTF_KEYUP, 0)

            ' Send a reverse Tab command:
            bVk = Keys.ShiftKey
            keybd_event(bVk, 0, 0, 0)
            bVk = Keys.tab
            keybd_event(bVk, 0, 0, 0)
            keybd_event(bVk, 0, KEYEVENTF_KEYUP, 0)
            bVk = Keys.ShiftKey
            keybd_event(bVk, 0, KEYEVENTF_KEYUP, 0)


            ' Start filtering for mouse clicks outside the popup
            filter.Popup = popup

        End Sub

        ''' &lt;summary&gt;
        ''' Subclasses the owning form's existing Window Procedure to enables
         the 
        ''' title bar to remain active when a popup is show, and to detect if
        ''' the user clicks onto another application whilst the popup is
         visible.
        ''' &lt;/summary&gt;
        ''' &lt;param name="m"&gt;Window Procedure Message&lt;/param&gt;
        Protected Overrides Sub WndProc(ByRef m As Message)

            MyBase.WndProc(m)

            If (Me.popupShowing) Then

                ' check for WM_ACTIVATE and WM_NCACTIVATE
                If (m.Msg = WM_NCACTIVATE) Then
                    ' Check if the title bar will made inactive:
                    If (m.WParam.Equals(IntPtr.Zero)) Then
                        ' If so reactivate it.
                        SendMessage(Me.Handle, WM_NCACTIVATE, 1, IntPtr.Zero)

                        ' Note it's no good to try and consume this message;
                        ' if you try to do that you'll end up with windows
                        ' that don't respond.
                    End If

                ElseIf (m.Msg = WM_ACTIVATEAPP) Then

                    ' Check if the application is being deactivated.
                    If (m.WParam.Equals(IntPtr.Zero)) Then
                        ' It is so cancel the popup:
                        ClosePopup()
                        ' And put the title bar into the inactive state:
                        PostMessage(Me.Handle, WM_NCACTIVATE, 0, IntPtr.Zero)
                    End If
                End If
            End If
        End Sub

        ''' &lt;summary&gt;
        ''' Called when the popup is being hidden.
        ''' &lt;/summary&gt;
        Public Sub ClosePopup()
            If (Me.popupShowing) Then
                If Not (skipClose) Then
                    ' Raise event to owner
                    OnPopupClosed(New PopupClosedEventArgs(m_popup))
                End If
                skipClose = False

                ' Make sure the popup is closed and we've cleaned
                ' up:
                m_owner.RemoveOwnedForm(m_popup)
                popupShowing = False
                m_popup.Close()
                ' No longer need to filter for clicks outside the
                ' popup.
                Application.RemoveMessageFilter(filter)

                ' If we did something from the popup which shifted
                ' focus to a new form, like showing another popup
                ' or dialog, then Windows won't know how to bring
                ' the original owner back to the foreground, so
                ' force it here:
                m_owner.Activate()

                ' Nothing out references for GC
                m_popup = Nothing
                m_owner = Nothing

            End If
        End Sub

        ''' &lt;summary&gt;
        ''' Raises the &lt;see cref="PopupClosed"/&gt; event.
        ''' &lt;/summary&gt;
        ''' &lt;param name="e"&gt;&lt;see cref="PopupClosedEventArgs"/&gt; describing the
        ''' popup form that is being closed.&lt;/param&gt;
        Protected Sub OnPopupClosed(ByVal e As PopupClosedEventArgs)
            RaiseEvent PopupClosed(Me, e)
        End Sub

        ''' &lt;summary&gt;
        ''' Raises the &lt;see cref="PopupCancel"/&gt; event.
        ''' &lt;/summary&gt;
        ''' &lt;param name="e"&gt;&lt;see cref="PopupCancelEventArgs"/&gt; describing the
        ''' popup form that about to be cancelled.&lt;/param&gt;
        Protected Sub OnPopupCancel(ByVal e As PopupCancelEventArgs)
            RaiseEvent PopupCancel(Me, e)
            If Not (e.Cancel) Then
                skipClose = True
            End If
        End Sub

        ''' &lt;summary&gt;
        ''' Default constructor.
        ''' &lt;/summary&gt;
        ''' &lt;remarks&gt;Use the &lt;see
         cref="System.Windows.Forms.NativeWindow.AssignHandle"/&gt;
        ''' method to attach this class to the form you want to show popups
         from.&lt;/remarks&gt;
        Public Sub New()
            filter = New PopupWindowHelperMessageFilter(Me)
        End Sub


        Private Sub m_popup_Closed(ByVal sender As Object, ByVal e As
         System.EventArgs) Handles m_popup.Closed
            ClosePopup()
        End Sub

        Private Sub filter_PopupCancel(ByVal sender As Object, ByVal e As
         PopupControlVB.vbAccelerator.Components.Controls.PopupCancelEventArgs)
         Handles filter.PopupCancel
            OnPopupCancel(e)
        End Sub
    End Class
#End Region

#Region "PopupWindowHelperMessageFilter"
    ''' &lt;summary&gt;
    ''' A Message Loop filter which detect mouse events whilst the popup form
     is shown
    ''' and notifies the owning &lt;see cref="PopupWindowHelper"/&gt; class when a
     mouse
    ''' click outside the popup occurs.
    ''' &lt;/summary&gt;
    Public Class PopupWindowHelperMessageFilter
        Implements IMessageFilter

        Private Const WM_LBUTTONDOWN As Integer = &amp;H201
        Private Const WM_RBUTTONDOWN As Integer = &amp;H204
        Private Const WM_MBUTTONDOWN As Integer = &amp;H207
        Private Const WM_NCLBUTTONDOWN As Integer = &amp;HA1
        Private Const WM_NCRBUTTONDOWN As Integer = &amp;HA4
        Private Const WM_NCMBUTTONDOWN As Integer = &amp;HA7

        ''' &lt;summary&gt;
        ''' Raised when the Popup Window is about to be cancelled.  The
        ''' &lt;see cref="PopupCancelEventArgs.Cancel"/&gt; property can be
        ''' set to &lt;c&gt;true&lt;/c&gt; to prevent the form from being cancelled.
        ''' &lt;/summary&gt;
        Public Event PopupCancel As PopupCancelEventHandler

        ''' &lt;summary&gt;
        ''' The popup form
        ''' &lt;/summary&gt;
        Private m_popup As Form = Nothing
        ''' &lt;summary&gt;
        ''' The owning &lt;see cref="PopupWindowHelper"/&gt; object.
        ''' &lt;/summary&gt;
        Private owner As PopupWindowHelper = Nothing

        ''' &lt;summary&gt;
        ''' Constructs a new instance of this class and sets the owning
        ''' object.
        ''' &lt;/summary&gt;
        ''' &lt;param name="owner"&gt;The &lt;see cref="PopupWindowHelper"/&gt; object
        ''' which owns this class.&lt;/param&gt;
        Public Sub New(ByVal owner As PopupWindowHelper)
            Me.owner = owner
        End Sub

        ''' &lt;summary&gt;
        ''' Gets/sets the popup form which is being displayed.
        ''' &lt;/summary&gt;
        Public Property Popup() As Form
            Get
                Return m_popup
            End Get
            Set(ByVal Value As Form)
                m_popup = Value
            End Set
        End Property

        ''' &lt;summary&gt;
        ''' Checks the message loop for mouse messages whilst the popup
        ''' window is displayed.  If one is detected the position is
        ''' checked to see if it is outside the form, and the owner
        ''' is notified if so.
        ''' &lt;/summary&gt;
        ''' &lt;param name="m"&gt;Windows Message about to be processed by the
        ''' message loop&lt;/param&gt;
        ''' &lt;returns&gt;&lt;c&gt;true&lt;/c&gt; to filter the message, &lt;c&gt;false&lt;/c&gt; otherwise.
        ''' This implementation always returns &lt;c&gt;false&lt;/c&gt;.&lt;/returns&gt;
        Public Function PreFilterMessage(ByRef m As Message) As Boolean _
                  Implements IMessageFilter.PreFilterMessage
            If Not (Me.Popup Is Nothing) Then
                Select Case m.Msg
                    Case WM_LBUTTONDOWN, WM_RBUTTONDOWN, WM_MBUTTONDOWN, _
                        WM_NCLBUTTONDOWN, WM_NCRBUTTONDOWN, WM_NCMBUTTONDOWN
                        OnMouseDown()
                End Select
            End If
            Return False
        End Function

        ''' &lt;summary&gt;
        ''' Checks the mouse location and calls the OnCancelPopup method
        ''' if the mouse is outside the popup form.      
        ''' &lt;/summary&gt;
        Private Sub OnMouseDown()

            ' Get the cursor location
            Dim cursorPos As Point = Cursor.Position
            ' Check if it is within the popup form
            If Not (Popup.Bounds.Contains(cursorPos)) Then
                ' If not, then call to see if it should be closed
                OnCancelPopup(New PopupCancelEventArgs(Popup, cursorPos))
            End If

        End Sub

        ''' &lt;summary&gt;
        ''' Raises the &lt;see cref="PopupCancel"/&gt; event.
        ''' &lt;/summary&gt;
        ''' &lt;param name="e"&gt;The &lt;see cref="PopupCancelEventArgs"/&gt; associated 
        ''' with the cancel event.&lt;/param&gt;
        Protected Sub OnCancelPopup(ByVal e As PopupCancelEventArgs)
            RaiseEvent PopupCancel(Me, e)
            If Not (e.Cancel) Then
                owner.ClosePopup()
                ' Clear reference for GC
                Popup = Nothing
            End If
        End Sub
    End Class
#End Region



End Namespace
</pre><p>&#160;</p><p>&#160;</p><p>&#160;</p><p>&#160;</p><p>&#160;</p><p>&#160;</p></td><td class="rightbar" valign="top" bgcolor="#C2BEB1">
<!-- No right bar content currently defined -->
</td></tr><tr class="footer"><td></td>
<td>
<p class="center">

<script type="text/javascript"><!--
google_ad_client = "pub-4690375644913255";
google_ad_width = 728;
google_ad_height = 90;
google_ad_format = "728x90_as";
google_ad_channel ="";
google_ad_type = "text_image";
google_color_border = "336699";
google_color_bg = "FFFFFF";
google_color_link = "0000FF";
google_color_url = "008000";
google_color_text = "000000";
//--></script>
<script type="text/javascript"
  src="http://pagead2.googlesyndication.com/pagead/show_ads.js">
</script>
<noscript>
<A HREF="..\..\..\..\..\vb\code\vbmedia\using_gdi_plus\index.html" ><IMG SRC="..\..\..\..\..\res\gdiplus.png" ALT="A GDI+ Library for VB - read and write PNG, JPG, TIF and GIF files, and manipulate bitmaps quickly" WIDTH=468 HEIGHT=60 BORDER=0></A>
</noscript>

</p>
</td>
<td></td>
</tr><tr><td colspan="3"><hr /></td></tr><tr class="footer"><td valign="top"><a href="..\..\..\..\..\index.html"><img width="125" height="25" src="..\..\..\..\..\res\vbaccelerator.png" alt="vbAccelerator Logo" /></a></td><td valign="bottom"><p class="nav"><a href="..\..\..\..\..\index.html">Home</a>&#160;.&#160;<a href="..\..\..\..\index.html">NET</a>&#160;.&#160;<a href="..\..\..\index.html">Code</a>&#160;.&#160;<a href="..\..\index.html">Controls</a>&#160;.&#160;<a href="..\index.html">Popup Windows</a>&#160;.&#160;<a href="article.html">Use .NET Forms as Popup Windows</a>&#160;.&#160;<a href="popup_form_demonstration.html">Popup Form Demonstration</a>&#160;.&#160;PopupControlVB_PopupWindowHelper.vb</p><br /><p class="nav"><a href="..\..\..\..\..\the_site\copyright\article.html">Copyright</a> &#169; 2003 Steve McMahon <a href="mailto:steve@vbaccelerator.com">steve@vbaccelerator.com</a>.  All rights reserved.<br />Last Updated: 2 November 2003</p></td><td></td></tr></table>
</body></html>