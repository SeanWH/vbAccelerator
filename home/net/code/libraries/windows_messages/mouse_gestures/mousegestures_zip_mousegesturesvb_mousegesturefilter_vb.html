<html lang="en" >
<head>
<title>vbAccelerator - Contents of code file: MouseGesturesVB_MouseGestureFilter.vb</title><meta http-equiv="Content-Type" content="text/html;charset=utf-8" /><meta name="AUTHOR" content="Steve McMahon" /><meta name="KEYWORDS" content=".NET, .NET Framework, C#, CSharp, VB, Visual Basic, VB5, VB6, Visual Basic, Active X, Controls, Components" /><meta name="DESCRIPTION" content="vbAccelerator - Contents of code file: MouseGesturesVB_MouseGestureFilter.vb" /><link rel="stylesheet" href="..\..\..\..\..\res\screen.css" media="SCREEN" /><link rel="stylesheet" href="..\..\..\..\..\res\print.css" media="PRINT" /></head><body>
<table>
<tr class="adbar" bgcolor="#83726a"><td colspan="3">
<!-- TF 468x60 JScript HORIZ NoPop code -->
<script language=javascript><!--
document.write('<scr'+'ipt language=javascript src="http://a.tribalfusion.com/j.ad?site=VBAcceleratorcom&adSpace=ROS&size=468x60&type=horiz&pop=0&requestID='+((new Date()).getTime() % 2147483648) + Math.random()+'"></scr'+'ipt>');
//-->
</script>
<noscript>
   <a href="javascript:window.alert(&quot;http://a.tribalfusion.com/i.click?site=VBAcceleratorcom&adSpace=ROS&size=468x60&requestID=1423477941\nThis link was not retrieved.&quot;)" target=_blank>
   <img src="http://cdn5.tribalfusion.com/media/493786.gif" 
                  width=468 height=60 border=0 alt="Click Here"></a>
</noscript>
<!-- TF 468x60 JScript HORIZ NoPop code -->
</td></tr><tr class="navbar"><td><a href="..\..\..\..\..\index.html"><img width="125" height="25" src="..\..\..\..\..\res\vbaccelerator.png" alt="vbAccelerator Logo" /></a></td><td valign="bottom"><p class="nav"><a href="..\..\..\..\..\index.html">Home</a>&#160;.&#160;<a href="..\..\..\..\index.html">NET</a>&#160;.&#160;<a href="..\..\..\index.html">Code</a>&#160;.&#160;<a href="..\..\index.html">Libraries</a>&#160;.&#160;<a href="..\index.html">Windows Messages</a>&#160;.&#160;<a href="article.html">Adding Mouse Gesture Support to .NET Windows Applications</a>&#160;.&#160;<a href="mousegestures.html">MouseGestures</a>&#160;.&#160;MouseGesturesVB_MouseGestureFilter.vb</p></td><td></td></tr><tr class="navbar"><td colspan="3"><hr /></td></tr><tr valign="top"><td class="sidebar"><img src="..\..\..\..\..\res\download.png" width="125" height="21" alt="Downloads" /><p class="nav"><a href="mousegestures.html"><img src="..\..\..\..\..\res\get.png" width="8" height="8" alt="Download Page" />MouseGestures</a> (154K)</p><br /><br /><img src="..\..\..\..\..\res\information.png" width="125" height="21" alt="Information" /><p class="nav">Zip:13128</p><p class="nav">&#160;&#160;<a href="..\..\..\..\..\..\linkto_asp\id=13128&type=zip&title=mousegestures_2ezip_5fmousegesturesvb_5fmousegesturefilter.html">Link to code Zip</a></p><p class="nav">Applies To:</p><p class="nav">&#160;&#160;VB5</p><p class="nav">&#160;&#160;VB6</p><p class="nav">Author:</p><p class="nav">&#160;&#160;<a href="mailto:steve@vbaccelerator.com">Steve McMahon</a></p><br /><br /><img src="..\..\..\..\..\res\bugtrak.png" width="125" height="21" alt="BugTrak System" /><p class="nav">No logged bugs.</p><br /><br /><img src="..\..\..\..\..\res\updates.png" width="125" height="21" alt="Updates" /><p class="nav"><img src="..\..\..\..\..\res\update.png" width="8" height="8" alt="Update" />23 Oct 2003<br />First Posted</p><br /><br /><img src="..\..\..\..\..\res\related.png" width="125" height="21" alt="Related Items" /><br /><br /><img src="..\..\..\..\..\res\search.png" width="125" height="21" alt="Search" />
<!-- Search Google -->
<form method="GET" action="http://www.google.com/custom">
<a href="javascript:window.alert(&quot;http://www.google.com/search\nThis link was not retrieved.&quot;)"><img src="..\..\..\..\..\..\..\www_google_com\logos\logo_40wht.gif" alt="Google" /></A><br />
<input type="text" name="q" size="31" maxlength="255" value="" class="search" /><br />
<input type="submit" name="sa" value="Search Google" class="search" />
<input type="hidden" name="cof" value="T:black;LW:330;ALC:990000;L:http://www.vbaccelerator.com/images/accl9.gif;LC:660000;LH:66;BGC:white;AH:left;VLC:999966;S:http://vbaccelerator.com/;GALT:999966;AWFID:67c7759ff27e5395;" />
<input type="hidden" name="domains" value="vbaccelerator.com" /><br />
<p class="nav"><input type="radio" name="sitesearch" value="" />Search WWW<br /></p>
<p class="nav"><input type="radio" name="sitesearch" value="vbaccelerator.com" checked="true" />vbAccelerator<br /></p>
</FORM>
<!-- Search Google -->
<br /><br />

<a href="..\..\..\..\..\the_site\newsite\article.html"><img src="..\..\..\..\..\res\newsite.png" width="125" height="129" alt="The New Site" /></a>
<br /><br />
</td><td width="100%" valign="top"><h1>vbAccelerator - Contents of code file: MouseGesturesVB_MouseGestureFilter.vb</h1><pre>Namespace vbAccelerator.Components.Win32


    ''' &lt;summary&gt;
    ''' Enumerated flag values for the mouse gestures supported by 
    ''' the MouseGesture class.
    ''' &lt;/summary&gt;
    &lt;FlagsAttribute()&gt; _
    Public Enum MouseGestureTypes As Integer
        ''' &lt;summary&gt;
        ''' No mouse gesture.
        ''' &lt;/summary&gt;
        NoGesture = &amp;H0
        ''' &lt;summary&gt;
        ''' Mouse Gesture move north
        ''' &lt;/summary&gt;
        NorthGesture = &amp;H1
        ''' &lt;summary&gt;
        ''' Mouse Gesture move south
        ''' &lt;/summary&gt;
        SouthGesture = &amp;H2
        ''' &lt;summary&gt;
        ''' Mouse Gesture move east
        ''' &lt;/summary&gt;
        EastGesture = &amp;H4
        ''' &lt;summary&gt;
        ''' Mouse Gesture move west
        ''' &lt;/summary&gt;
        WestGesture = &amp;H8
        ''' &lt;summary&gt;
        ''' Mouse Gesture move north-east
        ''' &lt;/summary&gt;
        NorthThenEastGesture = &amp;H10
        ''' &lt;summary&gt;
        ''' Mouse Gesture move south-east
        ''' &lt;/summary&gt;
        SouthThenEastGesture = &amp;H20
        ''' &lt;summary&gt;
        ''' Mouse Gesture move south-west
        ''' &lt;/summary&gt;
        SouthThenWestGesture = &amp;H40
        ''' &lt;summary&gt;
        ''' Mouse Gesture move north-west
        ''' &lt;/summary&gt;      
        NorthThenWestGesture = &amp;H80
        ''' &lt;summary&gt;
        ''' Mouse Gesture move north-east
        ''' &lt;/summary&gt;
        EastThenNorthGesture = &amp;H100
        ''' &lt;summary&gt;
        ''' Mouse Gesture move south-east
        ''' &lt;/summary&gt;
        EastThenSouthGesture = &amp;H200
        ''' &lt;summary&gt;
        ''' Mouse Gesture move south-west
        ''' &lt;/summary&gt;
        WestThenSouthGesture = &amp;H400
        ''' &lt;summary&gt;
        ''' Mouse Gesture move north-west
        ''' &lt;/summary&gt;      
        WestThenNorthGesture = &amp;H800
        ''' &lt;summary&gt;
        ''' All mouse gestures
        ''' &lt;/summary&gt;
        AllGestureTypes = &amp;HFFF
    End Enum

    ''' &lt;summary&gt;
    ''' Holds the arguments for a gesture event.  The &lt;c&gt;acceptGesture&lt;/c&gt;
    ''' property is used to tell the class which raises the message whether
    ''' the consuming application acknowledged the gesture and therefore to 
    ''' cancel the right mouse up event.
    ''' &lt;/summary&gt;
    Public Class MouseGestureEventArgs
        Inherits EventArgs

        Private m_gestureType As MouseGestureTypes
        Private m_gestureStartPosition As Point
        Private m_gestureEndPosition As Point
        Private m_acceptGesture As Boolean

        ''' &lt;summary&gt;
        ''' Gets the gesture type.
        ''' &lt;/summary&gt;
        Public ReadOnly Property GestureType() As MouseGestureTypes
            Get
                Return Me.m_gestureType
            End Get
        End Property

        ''' &lt;summary&gt;
        ''' Gets the mouse location for the point at which the gesture
        ''' was started, relative to the screen.
        ''' &lt;/summary&gt;
        Public ReadOnly Property GestureStartPosition() As Point
            Get
                Return Me.m_gestureStartPosition
            End Get
        End Property

        ''' &lt;summary&gt;
        ''' Gets the mouse location for the point at which the gesture
        ''' was ended, relative to the screen.
        ''' &lt;/summary&gt;
        Public ReadOnly Property GestureEndPosition() As Point
            Get
                Return Me.m_gestureEndPosition
            End Get
        End Property

        ''' &lt;summary&gt;
        ''' Gets/sets whether the gesture has been processed by the 
        ''' application.  By default, gestures are presumed to be unaccepted,
        ''' in which case the standard right mouse up behaviour will be 
        ''' activated.  By setting Me property to &lt;c&gt;true&lt;/c&gt; the right
        ''' mouse up is filtered and the application can process the gesture.
        ''' &lt;/summary&gt;
        Public Property AcceptGesture() As Boolean
            Get
                Return Me.m_acceptGesture
            End Get
            Set(ByVal Value As Boolean)
                Me.m_acceptGesture = Value
            End Set
        End Property

        ''' &lt;summary&gt;
        ''' Constructor
        ''' &lt;/summary&gt;
        ''' &lt;param name="gestureType"&gt;Type of gesture which was detected&lt;/param&gt;
        ''' &lt;param name="gestureStartPosition"&gt;Position of mouse relative to
         screen when gesture
        ''' was started&lt;/param&gt;
        ''' &lt;param name="gestureEndPosition"&gt;Position of mouse relative to
         screen when gesture
        ''' was completed&lt;/param&gt;
        Public Sub New( _
                ByVal gestureType As MouseGestureTypes, _
                ByVal gestureStartPosition As Point, _
                ByVal gestureEndPosition As Point _
            )
            Me.m_gestureType = gestureType
            Me.m_gestureStartPosition = gestureStartPosition
            Me.m_gestureEndPosition = gestureEndPosition
            Me.m_acceptGesture = False
        End Sub

    End Class

    ''' &lt;summary&gt;
    ''' Represents the method which handles the &lt;c&gt;MouseGesture&lt;/c&gt; event
    ''' raised by the &lt;c&gt;MouseGestureFilter&lt;/c&gt; class.
    ''' &lt;/summary&gt;
    Public Delegate Sub MouseGestureEventHandler(ByVal sender As Object, ByVal
     args As MouseGestureEventArgs)

    ''' &lt;summary&gt;
    ''' A Windows Message Loop filter which enables mouse gestures to 
    ''' be detected over any control or window.
    ''' &lt;/summary&gt;
    ''' &lt;remarks&gt;Controls which perform processing on Right Mouse
    ''' Down (rather than the standard Right Mouse Up) will still
    ''' perform the right mouse action regardless of whether a gesture
    ''' is made.&lt;/remarks&gt;
    Public Class MouseGestureFilter
        Implements IMessageFilter

        ''' &lt;summary&gt;
        ''' 
        ''' &lt;/summary&gt;
        Public Event MouseGesture As MouseGestureEventHandler

        Private Declare Auto Function PostMessage Lib "user32" ( _
            ByVal hwnd As IntPtr, ByVal wMsg As Integer, ByVal wParam As
             Integer, ByVal lParam As Integer) As Integer

        Private Const WM_ACTIVATE As Integer = &amp;H6
        Private Const WM_RBUTTONDOWN As Integer = &amp;H204
        Private Const WM_MOUSEMOVE As Integer = &amp;H200
        Private Const WM_RBUTTONUP As Integer = &amp;H205

        ''' &lt;summary&gt;
        ''' The default absolute number of pixels the mouse must travel
        ''' in any direction for the gesture to be acknowledged.
        ''' &lt;/summary&gt;
        Private Const DEFAULT_HYSTERESIS_PIXELS As Integer = 8

        ''' &lt;summary&gt;
        ''' How far does the mouse have to move before it is 
        ''' interpreted as a gesture?
        ''' &lt;/summary&gt;
        Protected hysteresis As Integer = DEFAULT_HYSTERESIS_PIXELS
        ''' &lt;summary&gt;
        ''' The configured mouse gesture types
        ''' &lt;/summary&gt;
        Private m_gestureTypes As MouseGestureTypes =
         MouseGestureTypes.NoGesture
        ''' &lt;summary&gt;
        ''' Whether we are checking for a gesture or not.
        ''' &lt;/summary&gt;
        Private checkingGesture As Boolean = False
        ''' &lt;summary&gt;
        ''' The recorded mouse gesture during gesture checking
        ''' &lt;/summary&gt;
        Private recordedGesture As MouseGestureTypes =
         MouseGestureTypes.NoGesture
        ''' &lt;summary&gt;
        ''' &lt;c&gt;ArrayList&lt;/c&gt; of mouse points recorded during gesture.
        ''' &lt;/summary&gt;
        Private gesture As ArrayList = Nothing


        ''' &lt;summary&gt;
        ''' Gets/sets the mouse gesture types to look for.
        ''' &lt;/summary&gt;
        Public Property GestureTypes() As MouseGestureTypes
            Get
                Return Me.m_gestureTypes
            End Get
            Set(ByVal Value As MouseGestureTypes)
                Me.m_gestureTypes = Value
            End Set
        End Property


        ''' &lt;summary&gt;
        ''' Prefilters all application messages to check whether
        ''' the message is a gesture or not.
        ''' &lt;/summary&gt;
        ''' &lt;param name="m"&gt;The Windows message to prefilter&lt;/param&gt;
        ''' &lt;returns&gt;&lt;c&gt;true&lt;/c&gt; if the message should be filtered (was a 
        ''' processed gesture), &lt;c&gt;false&lt;/c&gt; otherwise.&lt;/returns&gt;
        Public Function PreFilterMessage( _
                ByRef m As Message _
            ) As Boolean Implements
             System.Windows.Forms.IMessageFilter.PreFilterMessage

            Dim retValue As Boolean = False

            If (Me.m_gestureTypes &gt; 0) Then
                If (Me.checkingGesture) Then
                    If (m.Msg = WM_MOUSEMOVE) Then
                        AddToMouseGesture()
                    ElseIf (m.Msg = WM_RBUTTONUP) Then
                        retValue = EndMouseGesture()
                        If (retValue) Then
                            ' Windows will skip the next mouse down if we
                             consume
                            ' a mouse up.  m cannot be modified, despite being
                             byref,
                            ' so post a new one to a location which is
                             offscreen:
                            Dim offScreen As Integer = &amp;H7FFF7FFF
                            PostMessage(m.HWnd, WM_RBUTTONUP,
                             m.WParam.ToInt32(), offScreen)
                        End If
                    ElseIf (m.Msg = WM_ACTIVATE) Then
                        Me.checkingGesture = False
                    End If

                ElseIf (m.Msg = WM_RBUTTONDOWN) Then
                    BeginMouseGesture()
                End If
            End If

            Return retValue

        End Function

        ''' &lt;summary&gt;
        ''' 
        ''' &lt;/summary&gt;
        Private Sub BeginMouseGesture()
            gesture = New ArrayList()
            gesture.Add(Cursor.Position)
            Me.checkingGesture = True
        End Sub

        ''' &lt;summary&gt;
        ''' 
        ''' &lt;/summary&gt;
        Private Sub AddToMouseGesture()
            gesture.Add(Cursor.Position)
        End Sub

        ''' &lt;summary&gt;
        ''' 
        ''' &lt;/summary&gt;
        ''' &lt;returns&gt;&lt;/returns&gt;
        Private Function EndMouseGesture() As Boolean

            Me.checkingGesture = False

            Dim retValue As Boolean = False

            '' add the end point:
            gesture.Add(Cursor.Position)

            '' get start and end:
            Dim first As Point = gesture(0)
            Dim last As Point = gesture(gesture.Count - 1)

            '' check which directions we register a change in:
            Dim xDiff As Integer = first.X - last.X
            Dim yDiff As Integer = first.Y - last.Y

            Dim north, south, east, west As Boolean

            If (Math.Abs(yDiff) &gt; DEFAULT_HYSTERESIS_PIXELS) Then
                north = (yDiff &gt; 0)
                south = Not (north)
            End If
            If (Math.Abs(xDiff) &gt; DEFAULT_HYSTERESIS_PIXELS) Then
                west = (xDiff &gt; 0)
                east = Not (west)
            End If

            '' check for very narrow angles as these are probably not compound
             gestures
            If ((north Or south) And (east Or west)) Then
                If (Math.Abs(xDiff) &gt; Math.Abs(yDiff)) Then
                    If ((Math.Abs(xDiff) / (Math.Abs(yDiff) * 1.0)) &gt; 7.0) Then
                        north = False
                        south = False
                    End If
                Else
                    If ((Math.Abs(yDiff) / (Math.Abs(xDiff) * 1.0)) &gt; 7.0) Then
                        east = False
                        west = False
                    End If
                End If
            End If

            recordedGesture = MouseGestureTypes.NoGesture

            If (north Or south) Then
                If (east Or west) Then
                    ' compound gesture
                    recordedGesture = interpretCompoundGesture(first, last,
                     north, south, east, west)
                Else
                    ' vertical gesture:
                    If (north) Then
                        recordedGesture = MouseGestureTypes.NorthGesture
                    Else
                        recordedGesture = MouseGestureTypes.SouthGesture
                    End If
                End If
            ElseIf (east Or west) Then
                ' horizontal gesture
                If (east) Then
                    recordedGesture = MouseGestureTypes.EastGesture
                Else
                    recordedGesture = MouseGestureTypes.WestGesture
                End If
            End If

            If Not (recordedGesture = MouseGestureTypes.NoGesture) Then
                If Not ((GestureTypes And recordedGesture) = 0) Then
                    Dim args As MouseGestureEventArgs = New
                     MouseGestureEventArgs( _
                     recordedGesture, first, last)
                    RaiseEvent MouseGesture(Me, args)
                    retValue = args.AcceptGesture
                End If
            End If

            Return retValue
        End Function

        Private Function interpretCompoundGesture( _
            ByVal first As Point, ByVal last As Point, _
            ByVal north As Boolean, ByVal south As Boolean, ByVal east As
             Boolean, ByVal west As Boolean _
            ) As MouseGestureTypes

            Dim retValue As MouseGestureTypes = MouseGestureTypes.NoGesture

            ' draw a diagonal line between start &amp; end
            ' and determine if most points are y above 
            ' the line or not:
            Dim pointAbove As Integer = 0
            Dim pointBelow As Integer = 0
            Dim point As Point
            For Each point In gesture
                Dim diagY As Integer = ((point.X - first.X) * (first.Y -
                 last.Y)) / (first.X - last.X) + first.Y
                If (point.Y &gt; diagY) Then
                    pointAbove += 1
                Else
                    pointBelow += 1
                End If
            Next

            If (north) Then
                If (east) Then
                    If (pointAbove &gt; pointBelow) Then
                        retValue = MouseGestureTypes.EastThenNorthGesture
                    Else
                        retValue = MouseGestureTypes.NorthThenEastGesture
                    End If
                Else
                    If (pointAbove &gt; pointBelow) Then
                        retValue = MouseGestureTypes.WestThenNorthGesture
                    Else
                        retValue = MouseGestureTypes.NorthThenWestGesture
                    End If
                End If
            ElseIf (south) Then
                If (east) Then
                    If (pointAbove &gt; pointBelow) Then
                        retValue = MouseGestureTypes.SouthThenEastGesture
                    Else
                        retValue = MouseGestureTypes.EastThenSouthGesture
                    End If
                Else
                    If (pointAbove &gt; pointBelow) Then
                        retValue = MouseGestureTypes.SouthThenWestGesture
                    Else
                        retValue = MouseGestureTypes.WestThenSouthGesture
                    End If
                End If
            End If

            Return retValue

        End Function



        ''' &lt;summary&gt;
        ''' Constructs a default instance of Me class.  The class
        ''' checks for all &lt;c&gt;MouseGestureTypes&lt;/c&gt;.
        ''' &lt;/summary&gt;       
        Public Sub New()
            Me.m_gestureTypes = MouseGestureTypes.AllGestureTypes
        End Sub

        ''' &lt;summary&gt;
        ''' Constructs a new instance of Me class and starts checking for
        ''' the specified mouse gestures.
        ''' &lt;/summary&gt;
        ''' &lt;param name="gestureTypes"&gt;&lt;/param&gt;
        Public Sub New(ByVal gestureTypes As MouseGestureTypes)
            Me.m_gestureTypes = gestureTypes
        End Sub

    End Class

End Namespace
</pre><p>&#160;</p><p>&#160;</p><p>&#160;</p><p>&#160;</p><p>&#160;</p><p>&#160;</p></td><td class="rightbar" valign="top" bgcolor="#C2BEB1">
<!-- No right bar content currently defined -->
</td></tr><tr class="footer"><td></td>
<td>
<p class="center">

<script type="text/javascript"><!--
google_ad_client = "pub-4690375644913255";
google_ad_width = 728;
google_ad_height = 90;
google_ad_format = "728x90_as";
google_ad_channel ="";
google_ad_type = "text_image";
google_color_border = "336699";
google_color_bg = "FFFFFF";
google_color_link = "0000FF";
google_color_url = "008000";
google_color_text = "000000";
//--></script>
<script type="text/javascript"
  src="http://pagead2.googlesyndication.com/pagead/show_ads.js">
</script>
<noscript>
<A HREF="..\..\..\..\..\vb\code\libraries\compression\index.html" ><IMG SRC="..\..\..\..\..\res\compress.png" ALT="Create and read Zip files and compress your data using Zlib" WIDTH=468 HEIGHT=60 BORDER=0></A>
</noscript>

</p>
</td>
<td></td>
</tr><tr><td colspan="3"><hr /></td></tr><tr class="footer"><td valign="top"><a href="..\..\..\..\..\index.html"><img width="125" height="25" src="..\..\..\..\..\res\vbaccelerator.png" alt="vbAccelerator Logo" /></a></td><td valign="bottom"><p class="nav"><a href="..\..\..\..\..\index.html">Home</a>&#160;.&#160;<a href="..\..\..\..\index.html">NET</a>&#160;.&#160;<a href="..\..\..\index.html">Code</a>&#160;.&#160;<a href="..\..\index.html">Libraries</a>&#160;.&#160;<a href="..\index.html">Windows Messages</a>&#160;.&#160;<a href="article.html">Adding Mouse Gesture Support to .NET Windows Applications</a>&#160;.&#160;<a href="mousegestures.html">MouseGestures</a>&#160;.&#160;MouseGesturesVB_MouseGestureFilter.vb</p><br /><p class="nav"><a href="..\..\..\..\..\the_site\copyright\article.html">Copyright</a> &#169; 2003 Steve McMahon <a href="mailto:steve@vbaccelerator.com">steve@vbaccelerator.com</a>.  All rights reserved.<br />Last Updated: 1 November 2003</p></td><td></td></tr></table>
</body></html>